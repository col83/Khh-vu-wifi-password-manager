import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import java.util.Properties

plugins {
    alias(libs.plugins.aboutLibraries)
    alias(libs.plugins.android.application)
    alias(libs.plugins.koin.compiler)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.ksp)
    alias(libs.plugins.refine)
    alias(libs.plugins.room3)
    alias(libs.plugins.stability.analyzer)
}

kotlin {
    jvmToolchain(25)
    compilerOptions {
        progressiveMode = true
        languageVersion = KotlinVersion.KOTLIN_2_4
        apiVersion = KotlinVersion.KOTLIN_2_4
        jvmTarget = JvmTarget.JVM_25
    }
}

android {
    namespace = "io.github.wifi_password_manager"
    compileSdk { version = release(37) }

    defaultConfig {
        applicationId = "io.github.wifi_password_manager"
        minSdk { version = release(30) }
        targetSdk { version = release(37) }
        versionCode = 20
        versionName = "1.19"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField(
            "String", "SOURCE_CODE_URL", "\"https://github.com/Khh-vu/wifi-password-manager\""
        )
    }

    signingConfigs {
        val keystoreProperties = Properties().apply {
            val keystorePropertiesFile = rootProject.file("key.properties")
            if (keystorePropertiesFile.exists()) {
                keystorePropertiesFile.inputStream().use(::load)
            }
        }
        register("release") {
            storeFile = keystoreProperties["storeFile"]?.let { file(it) }
            storePassword = keystoreProperties["storePassword"]?.toString()
            keyAlias = keystoreProperties["keyAlias"]?.toString()
            keyPassword = keystoreProperties["keyPassword"]?.toString()
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            isDebuggable = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro",
            )

            signingConfig = signingConfigs.findByName("release")?.takeIf { it.storeFile != null }
        }
        debug {
            isMinifyEnabled = false
            isShrinkResources = false
            isDebuggable = true

            applicationIdSuffix = "debug"
            versionNameSuffix = "-debug"

            buildConfigField("Boolean", "LOG_ENABLED", "true")
        }
    }
    buildFeatures {
        aidl = true
        compose = true
        buildConfig = true
    }
    packaging {
        resources { excludes += "/META-INF/{AL2.0,LGPL2.1}" }
        jniLibs {
            keepDebugSymbols += setOf(
                "**/libandroidx.graphics.path.so", "**/libdatastore_shared_counter.so"
            )
        }
    }
    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }
    androidResources {
        @Suppress("UnstableApiUsage")
        generateLocaleConfig = true
    }
}

composeCompiler {
    stabilityConfigurationFiles.addAll(rootProject.layout.projectDirectory.file("stability_config.conf"))
    reportsDestination = layout.buildDirectory.dir("compose_compiler")
    metricsDestination = layout.buildDirectory.dir("compose_compiler")
}

composeStabilityAnalyzer {
    stabilityConfigurationFiles.addAll(rootProject.layout.projectDirectory.file("stability_config.conf"))
    stabilityValidation { outputDir = layout.buildDirectory.dir("stability") }
}

dependencies {
    // AndroidX
    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.biometric.compose)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.core.splashscreen)
    implementation(libs.androidx.datastore)
    implementation(libs.androidx.lifecycle.process)
    implementation(libs.androidx.material3)
    implementation(libs.androidx.navigation3.runtime)
    implementation(libs.androidx.navigation3.ui)
    implementation(libs.androidx.runtime)
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.work.runtime.ktx)
    implementation(libs.room3.runtime)
    implementation(platform(libs.androidx.compose.bom))
    ksp(libs.room3.compiler)

    // Material Components
    implementation(libs.material.components)

    // FileKit
    implementation(libs.filekit.core)
    implementation(libs.filekit.dialogs)

    // JetBrains
    implementation(libs.adaptive)
    implementation(libs.adaptive.navigation3)
    implementation(libs.kotlinx.serialization.json)
    implementation(libs.lifecycle.viewmodel.compose)
    implementation(libs.lifecycle.viewmodel.navigation3)

    // Privileged
    compileOnly(libs.stub)
    compileOnly(projects.hiddenApi)
    implementation(libs.hiddenapibypass)
    implementation(libs.libsu.core)
    implementation(libs.libsu.service)
    implementation(libs.refine.runtime)
    implementation(libs.shizuku.api)
    implementation(libs.shizuku.provider)

    // Koin
    implementation(libs.koin.androidx.workmanager)
    implementation(libs.koin.annotations)
    implementation(libs.koin.compose)
    implementation(libs.koin.compose.viewmodel)
    implementation(libs.koin.core)
    implementation(platform(libs.koin.bom))

    // QR Code
    implementation(libs.qrose)

    // AboutLibraries
    implementation(libs.aboutlibraries.compose.core)
    implementation(libs.aboutlibraries.compose.m3)
    implementation(libs.aboutlibraries.core)

    // Testing
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.ui.test.junit4)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    testImplementation(libs.junit)

    // Debug
    debugImplementation(libs.androidx.ui.test.manifest)
    debugImplementation(libs.androidx.ui.tooling)
    // debugImplementation(libs.leakcanary.android)
}

room3 {
    schemaDirectory("$projectDir/schemas")
}
